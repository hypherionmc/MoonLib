subprojects {

    apply plugin: 'java'
    version = project.version

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    java.withSourcesJar()
    java.withJavadocJar()

    ext {
        release = project.properties['release'] ?: false
    }

    // Jenkins
    if (System.getenv('BUILD_NUMBER') != null && project.release == false) {
        version = version + "." + System.getenv('BUILD_NUMBER') + "d"
    }

    jar {
        manifest {
            attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : project.jar.archiveVersion,
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_author,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestampe'              : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Build-On-Minecraft'      : minecraft_version
            ])
        }
    }

    clean {
        delete "$rootDir/artifacts"
    }

    repositories {
        mavenCentral()
        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven {
            name = 'HypherionMC Maven'
            url = 'https://maven.firstdarkdev.xyz/releases/'
        }
    }


    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = 17
    }

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    task copyAllArtifacts(type: Copy) {
        from "$buildDir/libs"
        into "$rootDir/artifacts"
        include("*.jar")
    }

    build.finalizedBy(copyAllArtifacts);
}
